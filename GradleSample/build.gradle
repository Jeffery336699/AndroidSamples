// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        maven {//本地Maven仓库地址
            url uri('./repos')
        }
        google()
        jcenter()
        
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
        classpath 'com.happy.plugin:customPlugin:1.0.0'
        classpath 'com.happy.plugin:transformPlugin:1.0.0'
//        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/**-----------------Project相关示例------------------*/
defaultTasks 'clean' // 设置默认Task
ext { // 设置额外参数
    hello = 'Welcome to Gradle'
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    // 给所有项目都添加一个任务：helloTask
    task helloTask {
        doLast { task ->
            println "I'm ${task.project.name}"
        }
    }
}

subprojects {
    helloTask { // 给所有子项目中的helloTask任务增加一个doFirst回调
        doFirst {
            println "I'm the task in sub project, doFirst"
        }
    }

    afterEvaluate { project ->
        println "it's after evaluate..."
        helloTask.configure {
            doLast {
                println "configure task after evaluate"
            }
        }
    }
}

// 给所有的子项目（除了app项目）的helloTask配置doLast
configure(subprojects.findAll{ it.name != 'app' }) {
    helloTask {
        doLast {
            println "i am not app module, i configure doLast"
        }
    }
}